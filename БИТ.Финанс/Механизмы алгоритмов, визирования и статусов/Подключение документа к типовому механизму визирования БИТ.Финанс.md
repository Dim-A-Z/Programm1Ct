###### Навигация: *[Главная](https://github.com/Dim-A-Z/Programm1Ct/wiki/Главная)* | *[БИТ.Финанс](https://github.com/Dim-A-Z/Programm1Ct/wiki/БИТ.Финанс)* | *[Механизмы алгоритмов, визирования и статусов](https://github.com/Dim-A-Z/Programm1Ct/tree/main/БИТ.Финанс/Механизмы%20алгоритмов%2C%20визирования%20и%20статусов)*

# Подключение документа к типовому механизму визирования БИТ.Финанс

Для подключения нового документа к типовому механизму визирования БИТ.Финанс выполняем следующие шаги:

### Изменяем типовые объекты

Добавляем тип **<u>ДокументОбъект.ИмяНовогоДокумента</u>** в подписки на события:
+ ПодпискаНаСобытие.бит_ПередЗаписьюДокументаДляУстановкиСтатуса.Источник
+ ПодпискаНаСобытие.бит_ПриЗаписиДляУстановкиСтатуса.Источник

Добавляем тип **<u>ДокументСсылка.ИмяНовогоДокумента</u>** в определяемые типы:
+ ОпределяемыйТип.бит_ОбъектыВизирования.Тип
+ ОпределяемыйТип.бит_ОбъектыСоСтатусами.Тип

### Дорабатываем модуль объекта нового документа
Если у документа установлено свойство `Проведение` = `Разрешить`, тогда в модуль объекта нужно добавить код в обработчик проведения документа.
```1C Enterprise
#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
    бит_ВизированиеСервер.УстановитьСтатус(ЭтотОбъект);
    
КонецПроцедуры

#КонецОбласти
```

### Дорабатываем модуль менеджера нового документа
Необходимо добавить метод `бит_НовыйСтатусОбъекта`. Если имя документа задано без префикса `бит_`, то имя метода должно быть указано с ним. Если добавляется документ с префиксом `бит_`, то метод должен быть назван `НовыйСтатусОбъекта`.
```1C Enterprise
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Функция определяет новый статус объекта.
// Механизм определения статусов может быть переопределен алгоритмами.
//
// Параметры:
//  ТекущийОбъект - ДокументОбъект.бит_ПроектДоговора.
//  ПараметрыУстановкиСтатуса - Структура - состав полей см. в функции бит_ВизированиеСервер.НовыеПараметрыУстановкиСтатуса.
//
// Возвращаемое значение:
//   СправочникСсылка.бит_СтатусыОбъектов - новый статус объекта.
//
Функция бит_НовыйСтатусОбъекта(ТекущийОбъект, ПараметрыУстановкиСтатуса) Экспорт
	
	Возврат бит_ВизированиеСервер.НовыйСтатусПоСтандартномуАлгоритму(ТекущийОбъект, ПараметрыУстановкиСтатуса);
	
КонецФункции

#КонецОбласти

#КонецЕсли
```

### Разрабатываем формы для документа
##### Форма списка/выбора
Для вывода информации по статусам в форме списка/выбора необходимо сделать произвольный запрос, где основную таблицу Документ.ИмяНовогоДокумента соединить необязательным левым соединением с регистром сведений бит_СтатусыОбъектов
```1C Enterprise
ВЫБРАТЬ РАЗРЕШЕННЫЕ
	Документ.Ссылка КАК Ссылка,
	Документ.Номер КАК Номер,
	Документ.Дата КАК Дата,
	...
	ЕСТЬNULL(СтатусыОбъектов.Статус, ЗНАЧЕНИЕ(Справочник.бит_СтатусыОбъектов.ПустаяСсылка)) КАК Статус,
	ЕСТЬNULL(СтатусыОбъектов.ДатаИзмененияСтатуса, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаИзмененияСтатуса
ИЗ
	Документ.ИмяНовогоДокумента КАК Документ
		{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бит_СтатусыОбъектов КАК СтатусыОбъектов
		ПО Документ.Ссылка = СтатусыОбъектов.Объект
			И (СтатусыОбъектов.ВидСтатуса = ЗНАЧЕНИЕ(Перечисление.бит_ВидыСтатусовОбъектов.Статус))}
```
##### Форма документа
Для работы со статусами на форме необходимо:
+ Добавить реквизит формы **Статус** с типом `СправочникСсылка.бит_СтатусыОбъектов`
+ Добавить элемент формы **Статус**:
	+ Вид: `Поле надписи`
	+ Путь к данным: `Статус`
	+ Гиперссылка: `✔`
	+ ЦветТекстаЗаголовка: `стиль: Цвет заголовка статуса (БИТ)`
	+ Нажатие: `ИнформацияСтатусНажатие`
+ Добавить код обработчиков:
	+ обработчики формы `ПриСозданииНаСервере`, `ОбработкаОповещения`, `ПриЧтенииНаСервере`, `ПослеЗаписиНаСервере`
	+ обработчик нажатия элемента **Статус** `ИнформацияСтатусНажатие`
	+ служебную процедуру `ОбработкаОповещенияИзмененияСтатуса`
```1C Enterprise
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	бит_НастройкиДоступностиЭлементовФорм.ПодготовитьФорму(ЭтотОбъект, Объект, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененСтатус" И Источник = УникальныйИдентификатор Тогда
		ОбработкаОповещенияИзмененияСтатуса();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	бит_НастройкиДоступностиЭлементовФорм.ПодготовитьФорму(ЭтотОбъект, Объект);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	бит_НастройкиДоступностиЭлементовФорм.ПодготовитьФорму(ЭтотОбъект, Объект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИнформацияСтатусНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Фильтр 		   = Новый Структура("Объект", Объект.Ссылка);
	ПараметрыФормы = Новый Структура("Отбор", Фильтр);
	Открытьформу("РегистрСведений.бит_ИсторияИзмененияСтатусовОбъектов.ФормаСписка", 
		ПараметрыФормы, ЭтотОбъект, ЭтотОбъект,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбработкаОповещенияИзмененияСтатуса()
	
	Прочитать();
	
КонецПроцедуры

#КонецОбласти
```

### Выполняем действия в режиме предприятия
+ Обновить справочник **Объекты системы** с помощью команды формы списка `Обновить объекты системы`. ссылка на форму - `e1cib/list/Справочник.бит_ОбъектыСистемы`
  
  ![ScreenShot команды](https://github.com/Dim-A-Z/Programm1Ct/blob/main/ScreenShots/ScreenShot_0001.png)
+ Выполнить вызов следующего метода регистра сведений **бит_НастройкиОбъектовВизирования**
 `РегистрыСведений.бит_НастройкиОбъектовВизирования.ЗаполнитьНастройкиОбъектовВизированияПоУмолчанию();`

###### Навигация: *[Главная](https://github.com/Dim-A-Z/Programm1Ct/wiki/Главная)* | *[БИТ.Финанс](https://github.com/Dim-A-Z/Programm1Ct/wiki/БИТ.Финанс)* | *[Механизмы алгоритмов, визирования и статусов](https://github.com/Dim-A-Z/Programm1Ct/tree/main/БИТ.Финанс/Механизмы%20алгоритмов%2C%20визирования%20и%20статусов)*
